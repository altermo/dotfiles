#!/bin/fish

argparse -N1 -X1 'f/force' -- $argv;or return

set trash_dir "$HOME/.local/share/Trash"
set trash_info "$trash_dir/info"
set trash_files "$trash_dir/files"

set DEFAULT_LS 'ls -aF'
set DEFAULT_CAT 'bat -pp --color=always'

if not test -d "$trash_dir"
    echo "Trash dir doesn't exist" >&2
    exit 1
else if not test -d "$trash_files";or not test -d "$trash_info"
    echo "Trash subdir doesn't exist" >&2
    exit 1
end
switch $argv
    case clean-old
        for file in (fd . "$trash_info" --max-depth 1 --changed-before 7d -u|xargs -I_ basename _ .trashinfo)
            rm "$trash_info/$file.trashinfo"
            or return
            rm -rf "$trash_files/$file"
        end
    case rm
        for file in (fd . "$trash_info" --max-depth 1 -u -0|xargs -0 -I_ basename _ .trashinfo|fzf --multi --preview "[ -d $trash_files/{} ]&&$DEFAULT_LS $trash_files/{}||$DEFAULT_CAT $trash_files/{}")
            rm "$trash_info/$file.trashinfo"
            or return
            rm -rf "$trash_files/$file"
        end
    case '*'
        echo "Unknown subcommand $argv" >&2
        exit 1
end
